plugins {
  id 'io.franzbecker.gradle-lombok' version '1.14' // Last to support Java 7

  id 'com.jfrog.artifactory' version '4.8.1'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'org.unbroken-dome.test-sets' version '2.2.1'
  id 'com.github.ben-manes.versions' version '0.27.0'

  // Not applying google java format by default because it gets confused by stray java build
  // files in 'workspace' build directory in CI
  id 'com.github.sherter.google-java-format' version '0.8' apply false
  id 'com.dorongold.task-tree' version '1.4'
}

def isCI = System.getenv("CI") != null

allprojects {
  group = 'com.datadoghq'
  version = '0.39.0'

  if (isCI) {
    buildDir = "${rootDir}/workspace/${projectDir.path.replace(rootDir.path, '')}/build/"
  }

  apply from: "${rootDir}/gradle/dependencies.gradle"
  apply from: "${rootDir}/gradle/util.gradle"
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

description = 'dd-trace-java'

tasks.register("latestDepTest")

// Applied here to allow publishing of artifactory build info
apply from: "${rootDir}/gradle/publish.gradle"

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

allprojects {
  tasks.withType(JavaForkOptions).configureEach {
    maxHeapSize = System.properties["datadog.forkedMaxHeapSize"]
    minHeapSize = System.properties["datadog.forkedMinHeapSize"]
    jvmArgs "-XX:ErrorFile=/tmp/hs_err_pid%p.log"
  }
}
