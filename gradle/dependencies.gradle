def groovyVer = "2.5.8"
def spockGroovyVer = groovyVer.replaceAll(/\.\d+$/, '')

ext {
  versions = [
    opentracing: '0.32.0',

    slf4j      : "1.7.29",
    guava      : "20.0", // Last version to support Java 7

    // When upgrading for security fixes, ensure corresponding change is reflected in jmxfetch.
    jackson    : "2.10.0", // https://nvd.nist.gov/vuln/detail/CVE-2019-16942 et al

    spock      : "1.3-groovy-$spockGroovyVer",
    groovy     : groovyVer,
    logback    : "1.2.3",
    lombok     : "1.18.10",
    bytebuddy  : "1.10.4",
    scala      : "2.11.12",  // Last version to support Java 7 (2.12+ require Java 8+)
    kotlin     : "1.3.50",
    coroutines : "1.3.0"
  ]

  deps = [
    // OpenTracing
    opentracingApi : dependencies.create(group: 'io.opentracing', name: 'opentracing-api', version: versions.opentracing),
    opentracing    : [
      dependencies.create(group: 'io.opentracing', name: 'opentracing-api', version: versions.opentracing),
      dependencies.create(group: 'io.opentracing', name: 'opentracing-noop', version: versions.opentracing),
      dependencies.create(group: 'io.opentracing', name: 'opentracing-util', version: versions.opentracing),
    ],
    opentracingMock: dependencies.create(group: 'io.opentracing', name: 'opentracing-mock', version: versions.opentracing),

    // General
    slf4j          : "org.slf4j:slf4j-api:${versions.slf4j}",
    guava          : "com.google.guava:guava:$versions.guava",
    jackson        : [
      dependencies.create(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson),
      dependencies.create(group: 'org.msgpack', name: 'jackson-dataformat-msgpack', version: '0.8.18'),
    ],
    bytebuddy      : dependencies.create(group: 'net.bytebuddy', name: 'byte-buddy', version: "${versions.bytebuddy}"),
    bytebuddyagent : dependencies.create(group: 'net.bytebuddy', name: 'byte-buddy-agent', version: "${versions.bytebuddy}"),
    autoservice    : [
      dependencies.create(group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc3'),
      dependencies.create(group: 'com.google.auto', name: 'auto-common', version: '0.8'),
      // These are the last versions that support guava 20.0.  Upgrading has odd interactions with shadow.
      dependencies.create(group: 'com.google.guava', name: 'guava', version: "${versions.guava}"),
    ],

    // Testing

    spock          : [
      dependencies.create("org.spockframework:spock-core:${versions.spock}", {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
      }),
      // Used by Spock for mocking:
      dependencies.create(group: 'org.objenesis', name: 'objenesis', version: '2.6') // Last version to support Java7
    ],
    groovy         : "org.codehaus.groovy:groovy-all:${versions.groovy}",
    testcontainers : "org.testcontainers:testcontainers:1.12.2",
    testLogging    : [
      dependencies.create(group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback),
      dependencies.create(group: 'org.slf4j', name: 'log4j-over-slf4j', version: versions.slf4j),
      dependencies.create(group: 'org.slf4j', name: 'jcl-over-slf4j', version: versions.slf4j),
      dependencies.create(group: 'org.slf4j', name: 'jul-to-slf4j', version: versions.slf4j),
    ],
    scala          : dependencies.create(group: 'org.scala-lang', name: 'scala-library', version: "${versions.scala}"),
    kotlin         : dependencies.create(group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: "${versions.kotlin}"),
    coroutines     : dependencies.create(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: "${versions.coroutines}"),
  ]
}
