// this project will run in isolation under the agent's classloader
buildscript {

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "net.bytebuddy:byte-buddy-gradle-plugin:${versions.bytebuddy}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}
plugins {
  id "com.github.johnrengelman.shadow"
}
apply from: "$rootDir/gradle/java.gradle"

Project instr_project = project
subprojects {
  afterEvaluate { Project subProj ->
    if (subProj.getPlugins().hasPlugin('java')) {
      subProj.archivesBaseName = 'opentelemetry-auto-' + subProj.archivesBaseName

      // Make it so all instrumentation subproject tests can be run with a single command.
      instr_project.tasks.test.dependsOn(subProj.tasks.test)

      instr_project.dependencies {
        implementation(project(subProj.getPath()))
      }
    }
  }
}

dependencies {
  implementation(project(':auto-tooling')) {
    exclude module: ':auto-bootstrap'
  }
}

configurations {
  // exclude bootstrap dependencies from shadowJar
  implementation.exclude module: deps.slf4j
  implementation.exclude group: 'org.slf4j'
  implementation.exclude group: 'io.opentelemetry', module: 'opentelemetry-api'
}

// need to perform shading in two steps in order to avoid shading java.util.logging.Logger
// in the java-util-logging instrumentation since that instrumentation needs to
// reference unshaded java.util.logging.Logger
// (java.util.logging.Logger shading is not needed in any of the instrumentation modules,
// but it is needed for the dependencies, e.g. guava, which use java.util.logging.Logger)
task shadowJarStep1(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {

  archiveClassifier = 'step1'
  destinationDirectory = file("${project.buildDir}/step1")

  configurations = [project.configurations.runtimeClasspath]

  dependencies {
    exclude(project(':instrumentation:java-util-logging'))
  }

  // rewrite dependencies calling Logger.getLogger
  relocate 'java.util.logging.Logger', 'io.opentelemetry.auto.bootstrap.PatchLogger'
}

shadowJar {

  dependsOn shadowJarStep1

  from {
    zipTree(shadowJarStep1.archiveFile)
  }

  mergeServiceFiles()

  exclude '**/module-info.class'

  dependencies {
    exclude(dependency("org.projectlombok:lombok:$versions.lombok"))
  }

  // Prevents conflict with other SLF4J instances. Important for premain.
  relocate 'org.slf4j', 'io.opentelemetry.auto.slf4j'

  duplicatesStrategy = DuplicatesStrategy.FAIL

  dependencies {
    exclude(project(':auto-bootstrap'))
  }

  // relocate OpenTelemetry API usage
  relocate "io.opentelemetry.OpenTelemetry", "io.opentelemetry.auto.shaded.io.opentelemetry.OpenTelemetry"
  relocate "io.opentelemetry.common", "io.opentelemetry.auto.shaded.io.opentelemetry.common"
  relocate "io.opentelemetry.context", "io.opentelemetry.auto.shaded.io.opentelemetry.context"
  relocate "io.opentelemetry.correlationcontext", "io.opentelemetry.auto.shaded.io.opentelemetry.correlationcontext"
  relocate "io.opentelemetry.internal", "io.opentelemetry.auto.shaded.io.opentelemetry.internal"
  relocate "io.opentelemetry.metrics", "io.opentelemetry.auto.shaded.io.opentelemetry.metrics"
  relocate "io.opentelemetry.trace", "io.opentelemetry.auto.shaded.io.opentelemetry.trace"

  // relocate OpenTelemetry API dependency usage
  relocate "io.grpc.Context", "io.opentelemetry.auto.shaded.io.grpc.Context"

  // this is for instrumentation on opentelemetry-api itself
  relocate "unshaded.io.opentelemetry", "io.opentelemetry"
  relocate "unshaded.io.grpc", "io.grpc"
}
