apply from: "${rootDir}/gradle/instrumentation.gradle"
apply from: "${rootDir}/gradle/test-with-scala.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

// This won't work until the akka and scala integrations are split into separate projects.
//muzzle {
//  pass {
//    coreJdk()
//  }
//}

testSets {
  slickTest
}

compileSlickTestGroovy {
  classpath += files(sourceSets.slickTest.scala.classesDirectory)
}

dependencies {
  // This is needed for Scala ForJoinTask/Pool instrumentation
  compileOnly deps.scala
  // This is needed for Akka ForJoinTask/Pool instrumentation
  compileOnly group: 'com.typesafe.akka', name: 'akka-actor_2.11', version: '2.5.0'

  testCompile project(':instrumentation:trace-annotation')

  slickTestCompile project(':instrumentation:java-concurrent')
  slickTestCompile project(':instrumentation:trace-annotation')
  slickTestCompile project(':instrumentation:jdbc')
  slickTestCompile deps.scala
  slickTestCompile group: 'com.typesafe.slick', name: 'slick_2.11', version: '3.2.0'
  slickTestCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

// Run Slick library tests along with the rest of unit tests
test.dependsOn slickTest

// Until custom source sets can use minJavaVersionForTests separately from other tests
// force this one test to run on java8 only
slickTest {
  onlyIf {
    executable == toExecutable(findJavaHome(JavaVersion.VERSION_1_8))
  }
}
