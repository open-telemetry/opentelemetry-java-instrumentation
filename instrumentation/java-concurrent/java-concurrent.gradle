apply from: "$rootDir/gradle/instrumentation.gradle"
apply from: "$rootDir/gradle/test-with-scala.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

// This won't work until the akka and scala integrations are split into separate projects.
//muzzle {
//  pass {
//    coreJdk()
//  }
//}

testSets {
  slickTest
}

compileSlickTestGroovy {
  classpath += files(sourceSets.slickTest.scala.classesDirectory)
}

dependencies {
  // This is needed for Scala ForJoinTask/Pool instrumentation
  compileOnly deps.scala
  // This is needed for Akka ForJoinTask/Pool instrumentation
  compileOnly group: 'com.typesafe.akka', name: 'akka-actor_2.11', version: '2.5.0'

  testImplementation project(':instrumentation:annotations')

  slickTestImplementation project(':instrumentation:java-concurrent')
  slickTestImplementation project(':instrumentation:annotations')
  slickTestImplementation project(':instrumentation:jdbc')
  slickTestImplementation deps.scala
  slickTestImplementation group: 'com.typesafe.slick', name: 'slick_2.11', version: '3.2.0'
  slickTestImplementation group: 'com.h2database', name: 'h2', version: '1.4.197'
}

// Run Slick library tests along with the rest of unit tests
test.dependsOn slickTest

// Until custom source sets can use minJavaVersionForTests separately from other tests
// force this one test to run on java8 only
slickTest {
  onlyIf {
    String java8Home = findJavaHome(JavaVersion.VERSION_1_8)
    java8Home != null && executable == toExecutable(java8Home)
  }
}
