// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/instrumentation.gradle"

muzzle {
  // Version 2.7.5 and 2.7.8 were not released properly and muzzle cannot test against it causing failure.
  // So we have to skip them resulting in this verbose setup.
  fail {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[,2.6.0)"
  }
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.6.0,2.7.5)"
  }
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.7.6,2.7.8)"
  }
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.7.9,3.0.0)"
  }
  fail {
    group = 'com.couchbase.client'
    module = 'couchbase-client'
    versions = "(,)"
  }
}

dependencies {
  implementation project(':instrumentation:rxjava-1.0')

  library group: 'com.couchbase.client', name: 'java-client', version: '2.6.0'

  testImplementation project(':instrumentation:couchbase:couchbase-testing')
  testImplementation group: 'com.couchbase.mock', name: 'CouchbaseMock', version: '1.5.19'

  testLibrary group: 'org.springframework.data', name: 'spring-data-couchbase', version: '3.1.0.RELEASE'
  testLibrary group: 'com.couchbase.client', name: 'encryption', version: '1.0.0'

  latestDepTestLibrary group: 'org.springframework.data', name: 'spring-data-couchbase', version: '3.1+'
  latestDepTestLibrary group: 'com.couchbase.client', name: 'java-client', version: '2.+'
}
