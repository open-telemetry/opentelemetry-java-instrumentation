// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/instrumentation.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

muzzle {
  // Version 2.7.5 and 2.7.8 were not released properly and muzzle cannot test against it causing failure.
  // So we have to skip them resulting in this verbose setup.
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.0.0,2.7.5)"
  }
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.7.6,2.7.8)"
  }
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.7.9,3.0.0)"
  }
  fail {
    group = 'com.couchbase.client'
    module = 'couchbase-client'
    versions = "(,)"
  }
}

configurations {
  testArtifacts
}

// Create test artifacts so couchbase-2.6 can reuse tests
artifacts {
  testArtifacts testJar
}

dependencies {
  compile project(':instrumentation:rxjava-1.0')

  compileOnly group: 'com.couchbase.client', name: 'java-client', version: '2.0.0'

  testCompile group: 'com.couchbase.mock', name: 'CouchbaseMock', version: '1.5.19'

  testCompile group: 'org.springframework.data', name: 'spring-data-couchbase', version: '2.0.0.RELEASE'
  // Earliest version that seems to allow queries with CouchbaseMock:
  testCompile group: 'com.couchbase.client', name: 'java-client', version: '2.5.0'

  latestDepTestCompile group: 'org.springframework.data', name: 'spring-data-couchbase', version: '3.+'
  latestDepTestCompile group: 'com.couchbase.client', name: 'java-client', version: '2.+'
  latestDepTestCompile group: 'com.couchbase.client', name: 'encryption', version: '+'
}
