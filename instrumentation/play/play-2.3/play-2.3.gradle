ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
  // Play doesn't work with Java 9+ until 2.6.12
  maxJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/instrumentation.gradle"
apply from: "$rootDir/gradle/test-with-scala.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  pass {
    group = 'com.typesafe.play'
    module = 'play_2.11'
    versions = '[2.3.0,2.4)'
    assertInverse = true
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.12'
    versions = '[,]'
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.13'
    versions = '[,]'
  }
}

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'com.typesafe.play', name: 'play_2.11', version: '2.3.0'

  testCompile project(':instrumentation:netty:netty-3.8')

  testCompile group: 'com.typesafe.play', name: 'play-java_2.11', version: '2.3.0'
  testCompile group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.3.0'
  testCompile(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.3.0') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-websocket'
  }

  latestDepTestCompile group: 'com.typesafe.play', name: 'play-java_2.11', version: '2.3.+'
  latestDepTestCompile group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.3.+'
  latestDepTestCompile(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.3.+') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-websocket'
  }
}

compileLatestDepTestGroovy {
  classpath = classpath.plus(files(compileLatestDepTestScala.destinationDir))
  dependsOn compileLatestDepTestScala
}
