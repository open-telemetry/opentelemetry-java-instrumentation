ext {
  // Play doesn't work with Java 9+ until 2.6.12
  maxJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/instrumentation.gradle"

muzzle {
  pass {
    group = 'com.typesafe.play'
    module = 'play_2.11'
    versions = '[2.4.0,2.6)'
    assertInverse = true
    // versions 2.3.9 and 2.3.10 depends on com.typesafe.netty:netty-http-pipelining:1.1.2
    // which does not exist
    skip('2.3.9', '2.3.10')
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.12'
    versions = '[,]'
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.13'
    versions = '[,]'
  }
}

dependencies {
  // TODO(anuraaga): Something about library configuration doesn't work well with scala compilation
  // here.
  compileOnly "com.typesafe.play:play_2.11:2.4.0"

  testInstrumentation project(':instrumentation:netty:netty-4.0:javaagent')
  testInstrumentation project(':instrumentation:netty:netty-4.1:javaagent')
  testInstrumentation project(':instrumentation:akka-http-10.0:javaagent')
  testInstrumentation project(':instrumentation:async-http-client:async-http-client-2.0:javaagent')

  // Before 2.5, play used netty 3.x which isn't supported, so for better test consistency, we test with just 2.5
  testLibrary "com.typesafe.play:play-java_2.11:2.5.0"
  testLibrary "com.typesafe.play:play-java-ws_2.11:2.5.0"
  testLibrary("com.typesafe.play:play-test_2.11:2.5.0") {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }

  latestDepTestLibrary "com.typesafe.play:play-java_2.11:2.5.+"
  latestDepTestLibrary "com.typesafe.play:play-java-ws_2.11:2.5.+"
  latestDepTestLibrary("com.typesafe.play:play-test_2.11:2.5.+") {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }
}

// async-http-client 2.0 does not work with Netty versions newer than this due to referencing an
// internal file.
if (!testLatestDeps) {
  configurations.configureEach {
    resolutionStrategy {
      eachDependency { DependencyResolveDetails details ->
        //specifying a fixed version for all libraries with io.netty' group
        if (details.requested.group == 'io.netty' && details.requested.name != "netty-bom" && details.requested.name != "netty") {
          details.useVersion "4.0.34.Final"
        }
      }
    }
  }
}
