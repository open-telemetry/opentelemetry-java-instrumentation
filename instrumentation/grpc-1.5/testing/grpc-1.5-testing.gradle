plugins {
  id "java-library"
  id "com.google.protobuf" version "0.8.13"
}

apply from: "$rootDir/gradle/java.gradle"

def grpcVersion = '1.5.0'

protobuf {
  protoc {
    // Download compiler rather than using locally installed version:
    artifact = 'com.google.protobuf:protoc:3.3.0'
  }
  plugins {
    grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
  }
  generateProtoTasks {
    all()*.plugins { grpc {} }
  }
}

dependencies {
  compileOnly(project(':instrumentation:grpc-1.5:library'))

  api project(':testing-common')

  api group: 'io.grpc', name: 'grpc-core', version: grpcVersion
  api group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
  api group: 'io.grpc', name: 'grpc-stub', version: grpcVersion

  implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

  implementation deps.guava

  implementation deps.groovy
  implementation deps.opentelemetryApi
  implementation deps.spock
}
