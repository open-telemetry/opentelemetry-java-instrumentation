// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
  retryTests = true
}

apply from: "$rootDir/gradle/instrumentation.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  pass {
    group = "io.netty"
    module = "netty-codec-http"
    versions = "[4.1.0.Final,)"
    assertInverse = true
  }
  pass {
    group = "io.netty"
    module = "netty-all"
    versions = "[4.1.0.Final,)"
    assertInverse = true
  }
  fail {
    group = "io.netty"
    module = "netty"
    versions = "[,]"
  }
  pass {
    group = "io.vertx"
    module = "vertx-core"
    versions = "[3.3.0,)"
    assertInverse = true
  }
}

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'io.netty', name: 'netty-codec-http', version: '4.1.0.Final'

  testImplementation project(':instrumentation:annotations')
  testImplementation group: 'io.netty', name: 'netty-codec-http', version: '4.1.0.Final'
  testImplementation group: 'org.asynchttpclient', name: 'async-http-client', version: '2.1.0'

  latestDepTestImplementation group: 'io.netty', name: 'netty-codec-http', version: '(,5.0)'
  // latest async-http-client incompatable with 5.0+ netty
  latestDepTestImplementation group: 'org.asynchttpclient', name: 'async-http-client', version: '+'
}

// We need to force the dependency to the earliest supported version because other libraries declare newer versions.
configurations.testImplementation {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      //specifying a fixed version for all libraries with io.netty' group
      if (details.requested.group == 'io.netty') {
        details.useVersion "4.1.0.Final"
      }
    }
  }
}
