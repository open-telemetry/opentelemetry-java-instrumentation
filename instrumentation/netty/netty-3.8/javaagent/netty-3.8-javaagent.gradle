apply from: "$rootDir/gradle/instrumentation.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  pass {
    group = "io.netty"
    module = "netty"
    versions = "[3.8.0.Final,4)"
    assertInverse = true
  }
  fail {
    group = "io.netty"
    module = "netty-all"
    versions = "[,]"
    // Could not find netty-tcnative-2.0.39.Final-${os.detected.classifier}.jar (io.netty:netty-tcnative:2.0.39.Final)
    skip("4.1.64.Final")
  }
}

dependencies {
  compileOnly "io.netty:netty:3.8.0.Final"

  testLibrary "io.netty:netty:3.8.0.Final"
  testLibrary "com.ning:async-http-client:1.8.0"

  latestDepTestLibrary "io.netty:netty:3.10.+"
  latestDepTestLibrary "com.ning:async-http-client:1.9.+"
}

// We need to force the dependency to the earliest supported version because other libraries declare newer versions.
configurations.testImplementation {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      //specifying a fixed version for all libraries with io.netty' group
      if (details.requested.group == 'io.netty') {
        details.useVersion "3.8.0.Final"
      }
    }
  }
}
