apply from: "${rootDir}/gradle/instrumentation.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  fail {
    group = "javax.ws.rs"
    module = "jsr311-api"
    versions = "[,]"
  }
  pass {
    group = "javax.ws.rs"
    module = "javax.ws.rs-api"
    versions = "[,]"
  }
}

testSets {
  latestDepTest {
    dirName = 'test'
  }

  resteasy31Test {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'

  testCompile project(':instrumentation:servlet:request-3.0')
  testCompile project(':instrumentation:jaxrs:jaxrs-2.0:jaxrs-2.0-jersey-2.0')
  testCompile project(':instrumentation:jaxrs:jaxrs-2.0:jaxrs-2.0-resteasy-3.0')
  testCompile project(':instrumentation:jaxrs:jaxrs-2.0:jaxrs-2.0-resteasy-3.1')

  // Jersey
  // First version with DropwizardTestSupport:
  testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '0.8.0'
  testCompile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.3'
  testCompile group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: '2.9.10'

  latestDepTestCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '1.+'

  // Resteasy
  testCompile group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '3.0.0.Final'

  resteasy31TestCompile(group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '3.1.0.Final') {
    force = true
  }

  latestDepTestCompile group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '+'
}

test.dependsOn resteasy31Test

// Until custom source sets can use minJavaVersionForTests separately from other tests
// force this one test to run on java8 only
resteasy31Test {
  onlyIf {
    executable == toExecutable(findJavaHome(JavaVersion.VERSION_1_8))
  }
}
