group = 'io.opentelemetry.javaagent'

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/publish.gradle"

// TODO this is not the desired state, only reflects current reality
minimumBranchCoverage = 0
minimumInstructionCoverage = 0

configurations {
  // classpath used by the instrumentation muzzle plugin
  instrumentationMuzzle {
    canBeConsumed = true
    canBeResolved = false
    extendsFrom implementation
  }
}

dependencies {
  implementation project(':javaagent-bootstrap')
  implementation project(':javaagent-extension-api')
  implementation project(':javaagent-api')
  implementation project(':instrumentation-api')

  implementation "io.opentelemetry:opentelemetry-api"
  implementation "io.opentelemetry:opentelemetry-api-metrics"
  implementation "io.opentelemetry:opentelemetry-sdk"
  implementation "io.opentelemetry:opentelemetry-sdk-extension-autoconfigure"
  implementation "io.opentelemetry:opentelemetry-sdk-metrics"
  implementation("io.opentelemetry:opentelemetry-extension-kotlin")
  implementation "io.opentelemetry:opentelemetry-extension-aws"
  implementation "io.opentelemetry:opentelemetry-extension-trace-propagators"
  implementation("io.opentelemetry:opentelemetry-sdk-extension-resources") {
    // exclude sdk-common to avoid bumping guava version
    exclude group: 'io.opentelemetry', module: 'opentelemetry-sdk-common'
  }

  implementation "io.opentelemetry:opentelemetry-exporter-jaeger"
  implementation "io.opentelemetry:opentelemetry-exporter-logging"
  implementation "io.opentelemetry:opentelemetry-exporter-otlp"
  implementation "io.opentelemetry:opentelemetry-exporter-otlp-metrics"

  implementation "io.opentelemetry:opentelemetry-exporter-prometheus"
  implementation "io.prometheus:simpleclient"
  implementation "io.prometheus:simpleclient_httpserver"

  implementation "io.opentelemetry:opentelemetry-exporter-zipkin"

  api "net.bytebuddy:byte-buddy"
  implementation "net.bytebuddy:byte-buddy-agent"
  annotationProcessor "com.google.auto.service:auto-service"
  implementation "com.google.auto.service:auto-service"
  implementation "org.slf4j:slf4j-api"
  implementation "com.google.guava:guava"

  implementation "io.grpc:grpc-netty-shaded:1.35.1"

  testImplementation project(':testing-common')
  testImplementation "org.assertj:assertj-core"
  testImplementation "org.mockito:mockito-core"

  instrumentationMuzzle sourceSets.main.output
}

test {
  filter {
    excludeTestsMatching 'HelperInjectionTest'
  }
}
// Needs a fresh classloader.
// https://github.com/open-telemetry/opentelemetry-java-instrumentation/issues/919
test.finalizedBy(tasks.register('testHelperInjection', Test) {
  filter {
    includeTestsMatching 'HelperInjectionTest'
  }
})
