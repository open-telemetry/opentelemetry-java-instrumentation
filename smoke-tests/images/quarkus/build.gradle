buildscript {
  dependencies {
    classpath("com.google.cloud.tools:jib-quarkus-extension-gradle:0.1.1")
  }
}

plugins {
  id "java"
  id "io.quarkus"
  id "com.google.cloud.tools.jib" version "3.1.4"
}

group = "io.opentelemetry"
version = "0.0.1-SNAPSHOT"

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  implementation enforcedPlatform("io.quarkus.platform:quarkus-bom:2.5.0.Final")
  implementation "io.quarkus:quarkus-resteasy"
}

compileJava {
  options.release = 8
}

def targetJDK = project.hasProperty("targetJDK") ? project.targetJDK : 11

def tag = findProperty("tag") ?: new Date().format("yyyyMMdd.HHmmSS")

jib {
  from.image = "bellsoft/liberica-openjdk-alpine:$targetJDK"
  to.image = "ghcr.io/open-telemetry/opentelemetry-java-instrumentation/smoke-test-quarkus:jdk$targetJDK-$tag"
  container {
    mainClass = 'bogus'  // to suppress Jib warning about missing main class
  }
  pluginExtensions {
    pluginExtension {
      implementation = 'com.google.cloud.tools.jib.gradle.extension.quarkus.JibQuarkusExtension'
    }
  }
}

tasks.jibDockerBuild.dependsOn quarkusBuild
