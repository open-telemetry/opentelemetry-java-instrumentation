plugins {
  id "org.gradle.playframework" version "0.9"
}

ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
  playVersion = "2.6.20"
  scalaVersion = System.getProperty("scala.binary.version", /* default = */ "2.12")
}

play {
  platform {
    playVersion = project.ext.playVersion
    scalaVersion = project.ext.scalaVersion
    javaVersion = JavaVersion.VERSION_1_8
  }
  injectedRoutesGenerator = true
}

repositories {
  jcenter()
  maven {
    name "lightbend-maven-releases"
    url "https://repo.lightbend.com/lightbend/maven-release"
  }
  ivy {
    name "lightbend-ivy-release"
    url "https://repo.lightbend.com/lightbend/ivy-releases"
    layout "ivy"
  }
}

apply from: "$rootDir/gradle/java.gradle"

description = 'Play Integration Tests.'

dependencies {
  implementation "com.typesafe.play:play-guice_$scalaVersion:$playVersion"
  implementation "com.typesafe.play:play-logback_$scalaVersion:$playVersion"
  implementation "com.typesafe.play:filters-helpers_$scalaVersion:$playVersion"

  testImplementation project(':smoke-tests')
}

tasks.withType(Test).configureEach {
  dependsOn 'stage'
}
