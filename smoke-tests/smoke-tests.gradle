apply from: "$rootDir/gradle/java.gradle"

description = 'smoke-tests'

dependencies {
  api deps.spock
  api project(':testing')

  implementation project(':auto-exporters:opentelemetry-auto-exporter-logging')
  implementation deps.slf4j
}

subprojects { subProject ->
  subProject.tasks.withType(Javadoc).configureEach { enabled = false }
  subProject.tasks.withType(Test).configureEach {
    dependsOn = [':opentelemetry-javaagent:shadowJar', ':auto-exporters:opentelemetry-auto-exporter-logging:shadowJar']

    doFirst {
      // Tests depend on this to know where to run things and what agent jar to use
      jvmArgs "-Dio.opentelemetry.smoketest.builddir=${buildDir}"
      jvmArgs "-Dio.opentelemetry.smoketest.agent.shadowJar.path=${project(':opentelemetry-javaagent').tasks.shadowJar.archivePath}"
      jvmArgs "-Dota.exporter.jar=${project(':auto-exporters:opentelemetry-auto-exporter-logging').tasks.shadowJar.archivePath}"
    }
  }
}
