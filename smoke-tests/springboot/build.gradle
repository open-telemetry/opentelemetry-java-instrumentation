plugins {
  id 'org.springframework.boot' version '2.3.2.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'java'
  id 'com.google.cloud.tools.jib' version '2.5.0'
}

group = 'io.opentelemetry'
version = '0.0.1-SNAPSHOT'

repositories {
  mavenCentral()
  jcenter()
  // this is only needed for the working against unreleased otel-java snapshots
  maven {
    url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
    content {
      includeGroup "io.opentelemetry"
    }
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'io.opentelemetry:opentelemetry-extension-auto-annotations:0.10.0-SNAPSHOT'
}

compileJava {
  options.release = 8
}

def targetJDK = project.hasProperty("targetJDK") ? project.targetJDK : 11

jib {
  container.creationTime = 'USE_CURRENT_TIMESTAMP'
  from.image = "bellsoft/liberica-openjdk-alpine:$targetJDK"
  to.image = "open-telemetry-docker-dev.bintray.io/java/smoke-springboot-jdk$targetJDK"
  to.tags = [new Date().format("yyyyMMdd.HHmmSS")]
}