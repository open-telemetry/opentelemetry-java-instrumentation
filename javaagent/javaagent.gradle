import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id "com.github.johnrengelman.shadow"
}

description = 'OpenTelemetry Javaagent'

group = 'io.opentelemetry.javaagent'

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/publish.gradle"

configurations {
  shadowInclude
}

jar {
  manifest {
    attributes(
      "Main-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
      "Agent-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
      "Premain-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
      "Can-Redefine-Classes": true,
      "Can-Retransform-Classes": true,
    )
  }
}

CopySpec isolateSpec(Collection<Project> projectsWithShadowJar) {
  return copySpec {
    from({ projectsWithShadowJar.tasks.shadowJar.collect { zipTree(it.archiveFile) } }) {
      // important to keep prefix 'inst' short, as it is prefixed to lots of strings in runtime mem
      into 'inst'
      exclude 'io/opentelemetry/javaagent/instrumentation/api/**/*.class'
      rename '(^.*)\\.class$', '$1.classdata'
      // Rename LICENSE file since it clashes with license dir on non-case sensitive FSs (i.e. Mac)
      rename '^LICENSE$', 'LICENSE.renamed'
    }
    //This allows every instrumentation to have their own classes that should go to bootstrap classloader
    from({ projectsWithShadowJar.tasks.shadowJar.collect { zipTree(it.archiveFile) } }) {
      include 'io/opentelemetry/javaagent/instrumentation/api/**/*.class'
    }
  }
}

//Includes everything needed for OOTB experience
shadowJar {
  dependsOn ':instrumentation:shadowJar'
  dependsOn ':javaagent-exporters:shadowJar'
  def projectsWithShadowJar = [project(':javaagent-exporters'), project(':instrumentation')]
  with isolateSpec(projectsWithShadowJar)

  // Exclude class files pulled in from the javaagent-exporters project that are also pulled in from the
  // instrumentation project.
  // Removing duplicates reduces jar size and can prevent issues when downstream projects repackage
  // the jar.
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

//Includes instrumentations, but not exporters
task lightShadow(type: ShadowJar) {
  from sourceSets.main.output

  dependsOn ':instrumentation:shadowJar'
  def projectsWithShadowJar = [project(':instrumentation')]
  with isolateSpec(projectsWithShadowJar)
}

publishing {
  publications {
    maven(MavenPublication) {
      artifact lightShadow
    }
  }
}

tasks.withType(ShadowJar).configureEach {
  configurations = [project.configurations.shadowInclude]
  mergeServiceFiles()

  manifest {
    inheritFrom project.tasks.jar.manifest
  }

  exclude '**/module-info.class'

  // Prevents conflict with other SLF4J instances. Important for premain.
  relocate 'org.slf4j', 'io.opentelemetry.javaagent.slf4j'
  // rewrite dependencies calling Logger.getLogger
  relocate 'java.util.logging.Logger', 'io.opentelemetry.javaagent.bootstrap.PatchLogger'

  // prevents conflict with library instrumentation
  relocate 'io.opentelemetry.instrumentation.api', 'io.opentelemetry.javaagent.shaded.instrumentation.api'

  // relocate OpenTelemetry API
  relocate "io.opentelemetry.api", "io.opentelemetry.javaagent.shaded.io.opentelemetry.api"
  relocate "io.opentelemetry.spi", "io.opentelemetry.javaagent.shaded.io.opentelemetry.spi"
  relocate "io.opentelemetry.context", "io.opentelemetry.javaagent.shaded.io.opentelemetry.context"
  relocate "io.opentelemetry.extension.trace.propagation", "io.opentelemetry.javaagent.shaded.io.opentelemetry.extension.trace.propagation"
}

dependencies {
  testImplementation project(':javaagent-bootstrap')
  testImplementation project(':javaagent-api')
  testImplementation project(':instrumentation-api')
  testImplementation project(':utils:test-utils')

  testImplementation deps.testLogging
  testImplementation deps.guava

  testImplementation 'io.opentracing.contrib.dropwizard:dropwizard-opentracing:0.2.2'

  shadowInclude project(path: ':javaagent-bootstrap')
}

tasks.withType(Test).configureEach {
  // Multi-threaded logging seems to be causing deadlocks with Gradle's log capture.
  //  jvmArgs "-Dio.opentelemetry.javaagent.slf4j.simpleLogger.defaultLogLevel=debug"
  //  jvmArgs "-Dorg.slf4j.simpleLogger.defaultLogLevel=debug"

  doFirst {
    // Defining here to allow jacoco to be first on the command line.
    jvmArgs "-javaagent:${shadowJar.archivePath}"
  }

  testLogging {
    events "started"
  }

  dependsOn shadowJar
}
assemble.dependsOn lightShadow
assemble.dependsOn shadowJar
