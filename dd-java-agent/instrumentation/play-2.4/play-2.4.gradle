apply from: "${rootDir}/gradle/java.gradle"
apply from: "${rootDir}/gradle/test-with-scala.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest
}

muzzle {
  pass {
    group = 'com.typesafe.play'
    module = 'play_2.11'
    versions = '[2.4.0,2.7.0-M1)'
  }
  pass {
    group = 'com.typesafe.play'
    module = 'play_2.12'
    versions = '[2.4.0,2.7.0-M1)'
  }
}

// Work around for https://github.com/gradle/gradle/issues/6735. Remove on gradle >4.10.1
compileLatestDepTestScala {
  analysisFiles.setFrom(files())
}

dependencies {
  compileOnly group: 'com.typesafe.play', name: 'play_2.11', version: '2.4.0'

  compile project(':dd-trace-api')
  compile project(':dd-java-agent:agent-tooling')
  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile group: 'org.scala-lang', name: 'scala-library', version: '2.11.12'
  testCompile group: 'com.typesafe.play', name: 'play_2.11', version: '2.4.0'
  testCompile group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.4.0'
  testCompile project(':dd-java-agent:testing')
  testCompile project(':dd-java-agent:instrumentation:java-concurrent')
  testCompile project(':dd-java-agent:instrumentation:trace-annotation')
  testCompile project(':dd-java-agent:instrumentation:akka-http-10.0')
  testCompile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'

  latestDepTestCompile group: 'org.scala-lang', name: 'scala-library', version: '2.11.12'
  latestDepTestCompile group: 'com.typesafe.play', name: 'play_2.11', version: '2.6.0'
  latestDepTestCompile group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.6.0'
}

compileLatestDepTestGroovy {
  classpath = classpath.plus(files(compileLatestDepTestScala.destinationDir))
  dependsOn compileLatestDepTestScala
}

testJava8Minimum += '*Play*Test*'
