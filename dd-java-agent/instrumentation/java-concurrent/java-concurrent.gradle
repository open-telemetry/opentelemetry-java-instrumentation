// Set properties before any plugins get loaded
project.ext {
  // Execute tests on all JVMs, even rare and outdated ones
  coreJavaInstrumentation = true
}

apply from: "${rootDir}/gradle/java.gradle"
apply from: "${rootDir}/gradle/test-with-scala.gradle"
apply from: "${rootDir}/gradle/test-with-kotlin.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  slickTest
}

compileSlickTestGroovy {
  classpath = classpath.plus(files(compileSlickTestScala.destinationDir))
  dependsOn compileSlickTestScala
}

dependencies {
  // This is needed for Scala ForJoinTask/Pool instrumentation
  compileOnly deps.scala
  // This is needed for Akka ForJoinTask/Pool instrumentation
  compileOnly group: 'com.typesafe.akka', name: 'akka-actor_2.11', version: '2.5.0'

  compile project(':dd-trace-api')
  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  testCompile project(':dd-java-agent:instrumentation:trace-annotation')

  slickTestCompile project(':dd-java-agent:testing')
  slickTestCompile project(':dd-java-agent:instrumentation:java-concurrent')
  slickTestCompile project(':dd-java-agent:instrumentation:trace-annotation')
  slickTestCompile project(':dd-java-agent:instrumentation:jdbc')
  slickTestCompile deps.scala
  slickTestCompile group: 'com.typesafe.slick', name: 'slick_2.11', version: '3.2.0'
  slickTestCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

// Run Slick library tests along with the rest of unit tests
test.dependsOn slickTest
