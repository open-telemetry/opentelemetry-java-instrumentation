apply from: "${rootDir}/gradle/java.gradle"
apply from: "${rootDir}/gradle/test-with-scala.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  slickTest
}

compileSlickTestGroovy {
  classpath = classpath.plus(files(compileSlickTestScala.destinationDir))
  dependsOn compileSlickTestScala
}

// Work around for https://github.com/gradle/gradle/issues/6735. Remove on gradle >4.10.1
compileSlickTestScala {
  analysisFiles.setFrom(files())
}

dependencies {
  compile project(':dd-trace-api')
  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  testCompile project(':dd-java-agent:instrumentation:trace-annotation')

  slickTestCompile project(':dd-java-agent:testing')
  slickTestCompile project(':dd-java-agent:instrumentation:java-concurrent')
  slickTestCompile project(':dd-java-agent:instrumentation:trace-annotation')
  slickTestCompile project(':dd-java-agent:instrumentation:jdbc')
  slickTestCompile group: 'org.scala-lang', name: 'scala-library', version: '2.11.12'
  slickTestCompile group: 'com.typesafe.slick', name: 'slick_2.11', version: '3.2.0'
  slickTestCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

// Run Slick library tests along with the rest of unit tests
test.dependsOn slickTest
