apply plugin: 'version-scan'

versionScan {
  group = "org.apache.kafka"
  module = "kafka-streams"
  versions = "[0.11.0.0,)"
  verifyPresent = [
    'org.apache.kafka.streams.state.internals.CacheFunction'        : null,
    'org.apache.kafka.streams.state.internals.InMemoryKeyValueStore': null,
  ]
}

apply from: "${rootDir}/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'org.apache.kafka', name: 'kafka-streams', version: '0.11.0.0'

  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  // Include kafka-clients instrumentation for tests.
  testCompile project(':dd-java-agent:instrumentation:kafka-clients-0.11')

  testCompile group: 'org.apache.kafka', name: 'kafka-clients', version: '0.11.0.0'
  testCompile group: 'org.apache.kafka', name: 'kafka-streams', version: '0.11.0.0'
  testCompile group: 'org.springframework.kafka', name: 'spring-kafka', version: '1.3.3.RELEASE'
  testCompile group: 'org.springframework.kafka', name: 'spring-kafka-test', version: '1.3.3.RELEASE'
  testCompile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.3'
}

configurations.latestDepTestCompile {
  resolutionStrategy {
    force group: 'org.apache.kafka', name: 'kafka-clients', version: '+'
    force group: 'org.apache.kafka', name: 'kafka-streams', version: '+'
    force group: 'org.springframework.kafka', name: 'spring-kafka', version: '+'
    force group: 'org.springframework.kafka', name: 'spring-kafka-test', version: '+'
  }
}
