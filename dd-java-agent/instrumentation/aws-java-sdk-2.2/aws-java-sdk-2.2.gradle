ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = "software.amazon.awssdk"
    module = "aws-core"
    versions = "[2.2.0,)"
    assertInverse = true
  }
}

apply from: "${rootDir}/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest
}

sourceSets {
  main_java8 {
    java.srcDirs "${project.projectDir}/src/main/java8"
  }
}

compileMain_java8Java {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

dependencies {
  main_java8CompileOnly group: 'software.amazon.awssdk', name: 'aws-core', version: '2.2.0'

  main_java8Compile project(':dd-java-agent:agent-tooling')

  main_java8Compile deps.bytebuddy
  main_java8Compile deps.opentracing

  compileOnly sourceSets.main_java8.compileClasspath
  compile sourceSets.main_java8.output

  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  // Include httpclient instrumentation for testing because it is a dependency for aws-sdk.
  testCompile project(':dd-java-agent:instrumentation:apache-httpclient-4')
  // Also include netty instrumentation because it is used by aws async client
  testCompile project(':dd-java-agent:instrumentation:netty-4.1')
  // Needed by netty async instrumentation
  testCompile project(':dd-java-agent:instrumentation:java-concurrent')
  testCompile group: 'software.amazon.awssdk', name: 'apache-client', version: '2.2.0'
  testCompile group: 'software.amazon.awssdk', name: 's3', version: '2.2.0'
  testCompile group: 'software.amazon.awssdk', name: 'rds', version: '2.2.0'
  testCompile group: 'software.amazon.awssdk', name: 'ec2', version: '2.2.0'

  latestDepTestCompile project(':dd-java-agent:testing')
  latestDepTestCompile project(':dd-java-agent:instrumentation:apache-httpclient-4')
  latestDepTestCompile project(':dd-java-agent:instrumentation:netty-4.1')
  latestDepTestCompile project(':dd-java-agent:instrumentation:java-concurrent')
  latestDepTestCompile group: 'software.amazon.awssdk', name: 'apache-client', version: '+'
  latestDepTestCompile group: 'software.amazon.awssdk', name: 's3', version: '+'
  latestDepTestCompile group: 'software.amazon.awssdk', name: 'rds', version: '+'
  latestDepTestCompile group: 'software.amazon.awssdk', name: 'ec2', version: '+'
}
