// compiling against 1.11.0, but instrumentation should work against 1.10.33 with varying effects,
// depending on the version's implementation. (i.e. DeleteOptionGroup may have less handlerCounts than
// expected in 1.11.84. Testing against 1.11.0 instead of 1.10.33 because the RequestHandler class
// used in testing is abstract in 1.10.33
// keeping base test version on 1.11.0 because RequestHandler2 is abstract in 1.10.33,
// therefore keeping base version as 1.11.0 even though the instrumentation probably
// is able to support up to 1.10.33
muzzle {
  pass {
    group = "com.amazonaws"
    module = "aws-java-sdk-core"
    versions = "[1.10.33,)"
    assertInverse = true
  }
}

apply from: "${rootDir}/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  // Features used in test_1_11_106 (builder) is available since 1.11.84, but
  // using 1.11.106 because of previous concerns with byte code differences
  // in 1.11.106, also, the DeleteOptionGroup request generates more spans
  // in 1.11.106 than 1.11.84.
  // We test older version in separate test set to test newer version and latest deps in the 'default'
  // test dir. Otherwise we get strange warnings in Idea.
  test_before_1_11_106 {
    dirName = 'test_before_1_11_106'
  }

  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-core', version: '1.11.0'

  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  // Include httpclient instrumentation for testing because it is a dependency for aws-sdk.
  testCompile project(':dd-java-agent:instrumentation:apache-httpclient-4')
  testCompile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.106'
  testCompile group: 'com.amazonaws', name: 'aws-java-sdk-rds', version: '1.11.106'
  testCompile group: 'com.amazonaws', name: 'aws-java-sdk-ec2', version: '1.11.106'

  test_before_1_11_106Compile(group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.0') {
    force = true
  }
  test_before_1_11_106Compile(group: 'com.amazonaws', name: 'aws-java-sdk-rds', version: '1.11.0') {
    force = true
  }
  test_before_1_11_106Compile(group: 'com.amazonaws', name: 'aws-java-sdk-ec2', version: '1.11.0') {
    force = true
  }

  latestDepTestCompile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '+'
  latestDepTestCompile group: 'com.amazonaws', name: 'aws-java-sdk-rds', version: '+'
  latestDepTestCompile group: 'com.amazonaws', name: 'aws-java-sdk-ec2', version: '+'
}

test.dependsOn test_before_1_11_106
