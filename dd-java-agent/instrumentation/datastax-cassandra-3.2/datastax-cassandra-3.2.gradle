// TODO: VersionScan plugin does not report which version failed, which is making it hard to get meaningful results out of this block.
// Once versionScan can report on which version failed, this can be enabled.
// The desire is to apply the instrumentation to cassandra-datastax 2.3 and beyond.

apply plugin: 'version-scan'

versionScan {
  group = "com.datastax.cassandra"
  module = "cassandra-driver-core"
  versions = "[3.2.0,)"
  verifyPresent = [
    "com.datastax.driver.core.Duration": null,
  ]
}

apply from: "${rootDir}/gradle/java.gradle"

// These classes use Cassandra 3 which requires Java 8. (Currently incompatible with Java 9.)
testJava8Only += '**/CassandraClientTest.class'

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.2.0'

  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  testCompile group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.2.0'
  testCompile group: 'org.cassandraunit', name: 'cassandra-unit', version: '3.1.3.2'

  latestDepTestCompile group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '+'
}
