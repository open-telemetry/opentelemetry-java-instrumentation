apply from: "${rootDir}/gradle/java.gradle"

minimumBranchCoverage = 0.5
minimumInstructionCoverage = 0.5
excludedClassesConverage += [
  'datadog.trace.agent.test.asserts.*Assert',
  'datadog.trace.agent.test.AgentTestRunner.ErrorCountingListener',
  'datadog.trace.agent.test.OkHttpUtils',
  'datadog.trace.agent.test.TestUtils',
  // Avoid applying jacoco instrumentation to classes instrumented by tested agent
  'context.ContextTestInstrumentation**',
]

dependencies {
  compile deps.bytebuddy
  compile deps.bytebuddyagent
  compile deps.slf4j
  compile deps.opentracing
  compile deps.spock
  compile deps.testLogging
  compile deps.guava

  compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '8.0.0.v20110901'

  compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.11.0'

  compile project(':dd-trace-ot')
  compile project(':dd-java-agent:agent-tooling')

  annotationProcessor deps.autoservice
  implementation deps.autoservice

  compile deps.groovy

  // test instrumenting java 1.1 bytecode
  testCompile group: 'net.sf.jt400', name: 'jt400', version: '6.1'

  // We have autoservices defined in test subtree, looks like we need this to be able to properly rebuild this
  testAnnotationProcessor deps.autoservice
  testImplementation deps.autoservice
}

// See comment for FieldBackedProviderFieldInjectionDisabledTest about why this hack is here
tasks.withType(Test) {
  if (name != "testDisabledFieldInjection") {
    exclude "context/FieldBackedProviderFieldInjectionDisabledTest.class"
  }
}

task testDisabledFieldInjection(type: Test) {
  systemProperties "dd.trace.runtime.context.field.injection": "false"
  include "context/FieldBackedProviderFieldInjectionDisabledTest.class"
}
test.dependsOn(testDisabledFieldInjection)
