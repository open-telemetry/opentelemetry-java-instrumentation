plugins {
  id "com.github.johnrengelman.shadow"
}

group = 'io.opentelemetry.instrumentation'

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/publish.gradle"

configurations {
  shadowInclude {
    canBeResolved = true
    canBeConsumed = false
  }
}

dependencies {
  compileOnly "com.github.ben-manes.caffeine:caffeine"
  shadowInclude("com.github.ben-manes.caffeine:caffeine") {
    exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    exclude group: 'org.checkerframework', module: 'checker-qual'
  }

  compileOnly "com.blogspot.mydailyjava:weak-lock-free"
  shadowInclude "com.blogspot.mydailyjava:weak-lock-free"
}

shadowJar {
  configurations = [project.configurations.shadowInclude]

  archiveClassifier.set("")

  relocate "com.github.benmanes.caffeine", "io.opentelemetry.instrumentation.api.internal.shaded.caffeine"
  relocate "com.blogspot.mydailyjava.weaklockfree", "io.opentelemetry.instrumentation.api.internal.shaded.weaklockfree"

  minimize()
}

jar {
  enabled = false

  dependsOn shadowJar
}
