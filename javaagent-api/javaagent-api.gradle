plugins {
  id 'com.intershop.gradle.javacc' version '4.0.0'
}

group = 'io.opentelemetry.javaagent'

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/publish.gradle"

project.ext.minimumBranchCoverage = 0.0
project.ext.minimumInstructionCoverage = 0.0

javacc {
  configs {
    normalizer {
      inputFile = file('src/main/javacc/SqlNormalizer.jj')
      packageName = 'io.opentelemetry.javaagent.instrumentation.api.db.normalizer'
      outputDir = file("${project.buildDir}/generated/javacc/sql")
    }
    simplifier {
      inputFile = file('src/main/javacc/SqlSimplifier.jj')
      packageName = 'io.opentelemetry.javaagent.instrumentation.api.db.normalizer'
      outputDir = file("${project.buildDir}/generated/javacc/sql")
    }
  }
}

tasks.withType(Checkstyle).configureEach {
  exclude '**/db/normalizer/*.java'
}

dependencies {
  api deps.opentelemetryApi
  compileOnly deps.opentelemetrySdk
  implementation deps.slf4j
  implementation project(':instrumentation-api')

  testImplementation project(':testing-common')
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.19.0'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '1.7.1'
}
