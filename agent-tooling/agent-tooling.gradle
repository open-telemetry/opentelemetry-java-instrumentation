apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/publish.gradle"

// TODO this is not the desired state, only reflects current reality
minimumBranchCoverage = 0
minimumInstructionCoverage = 0

configurations {
  // classpath used by the instrumentation muzzle plugin
  instrumentationMuzzle
}

dependencies {
  implementation project(':auto-bootstrap')
  implementation project(':auto-api')
  implementation project(':library-api')
  implementation project(':utils:thread-utils')

  implementation deps.opentelemetryApi
  implementation deps.opentelemetrySdk
  implementation deps.opentelemetrySdkAutoConfig
  implementation deps.opentelemetryTraceProps

  implementation group: 'com.blogspot.mydailyjava', name: 'weak-lock-free', version: '0.15'
  implementation deps.bytebuddy
  implementation deps.bytebuddyagent
  annotationProcessor deps.autoservice
  implementation deps.autoservice
  implementation deps.slf4j

  testImplementation project(':testing-common')

  instrumentationMuzzle sourceSets.main.output
  instrumentationMuzzle configurations.implementation
}

test {
  filter {
    excludeTestsMatching 'HelperInjectionTest'
  }
}

// Needs a fresh classloader.
// https://github.com/open-telemetry/opentelemetry-java-instrumentation/issues/919
def testHelperInjection = tasks.register('testHelperInjection', Test) {
  filter {
    includeTestsMatching 'HelperInjectionTest'
  }
}
test.dependsOn(testHelperInjection)
